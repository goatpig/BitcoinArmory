SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
SET(CMAKE_POSITION_INDEPENDENT_CODE ON)
SET(CMAKE_CXX_EXTENSIONS OFF)

IF (MINGW)
    ADD_LINK_OPTIONS(--static -static-libgcc -static-libstdc++)
ENDIF ()

ADD_SUBDIRECTORY(chacha20poly1305)
ADD_SUBDIRECTORY(hkdf)

SET(LIBARMORYCOMMON_INCLUDE_DIRECTORIES
    PUBLIC .
    PRIVATE hkdf
    PUBLIC ${LMDB_INCLUDE_DIRS}
    PUBLIC lmdbpp
    PRIVATE bech32/ref/c++
    PRIVATE chacha20poly1305
    PUBLIC ${LIBBTC_INCLUDE_DIR}/src
    PUBLIC ${LIBBTC_INCLUDE_DIR}/include
    PUBLIC ${LIBBTC_INCLUDE_DIR}/src/secp256k1/include
    PUBLIC ${LIBWEBSOCKETS_INCLUDE_DIRS}
    PUBLIC Signer
    PUBLIC Wallets
    PRIVATE Wallets/Accounts
    PRIVATE ${PROJECT_BINARY_DIR}/cppForSwig
    PUBLIC /usr/include
)

SET(LIBARMORYCOMMON_LINK_LIBRARIES
    chacha20poly1305
    hkdf
    ${LIBBTC_LIB}
    ${LMDB_LIB}
    ${CAPNPROTO_LIB}
    ${KJ_LIB}
)

FILE(GLOB LIBARMORYCOMMON_SOURCES *.cpp)
FILE(GLOB BLOCKDB_SOURCES BlockchainDatabase/*.cpp)
FILE(GLOB ACCOUNTS_SOURCES Wallets/Accounts/*.cpp)
FILE(GLOB SEEDS_SOURCES Wallets/Seeds/*.cpp)
FILE(GLOB SIGNER_SOURCES Signer/*.cpp)
FILE(GLOB BRIDGE_SOURCES BridgeApi/*.cpp)

FILE(GLOB WALLETS_SOURCES Wallets/*.cpp)
LIST(APPEND WALLETS_SOURCES
    ${LIBBTC_INCLUDE_DIR}/src/trezor-crypto/bip39.c
    ${LIBBTC_INCLUDE_DIR}/src/trezor-crypto/pbkdf2.c
    Wallets/random_buffer.c
)

LIST(APPEND LIBARMORYCOMMON_SOURCES
    lmdbpp/lmdbpp.cpp
    ${WALLETS_SOURCES}
    ${ACCOUNTS_SOURCES}
    ${SEEDS_SOURCES}
    ${SIGNER_SOURCES}
    ${BLOCKDB_SOURCES}
    gtest/NodeUnitTest.cpp
)

IF (ENABLE_TESTS)
    LIST(APPEND LIBARMORYCOMMON_SOURCES
        gtest/NodeUnitTest.cpp
    )
    ADD_DEFINITIONS(-DUNIT_TESTS)
ENDIF ()

IF (WIN32 OR MINGW)
    LIST(REMOVE_ITEM LIBARMORYCOMMON_SOURCES
        ${CMAKE_CURRENT_SOURCE_DIR}/SocketService_unix.cpp
    )
    SET(OS_SPECIFIC_LIBS
        uuid
        ole32
        ${WINSOCK_LIB}
    )
ELSE ()
    LIST(REMOVE_ITEM LIBARMORYCOMMON_SOURCES
        ${CMAKE_CURRENT_SOURCE_DIR}/SocketService_win32.cpp
    )
    SET(OS_SPECIFIC_LIBS gmp)
ENDIF ()

LIST(REMOVE_ITEM LIBARMORYCOMMON_SOURCES
    ${CMAKE_CURRENT_SOURCE_DIR}/main.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/old_not_very_good_tests.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/playground.cpp
)

SET(LIBARMORYCOMMON_COMPILE_DEFINITIONS
    PRIVATE
)

INCLUDE(CheckCXXSymbolExists)
find_package(PkgConfig)

## lws ##
check_cxx_symbol_exists(LWS_WITH_TLS lws_config.h LIBWEBSOCKETS_WITH_TLS)

check_cxx_symbol_exists(LWS_WITH_LIBUV lws_config.h LIBWEBSOCKETS_WITH_LIBUV)

IF (LIBWEBSOCKETS_WITH_LIBUV)
    pkg_check_modules(LIBUV REQUIRED libuv)

    LIST(APPEND LIBARMORYCOMMON_LINK_LIBRARIES
        ${LIBUV_LIBRARIES}
    )
ENDIF ()

check_cxx_symbol_exists(LWS_WITH_LIBEVENT lws_config.h LIBWEBSOCKETS_WITH_LIBEVENT)

IF (LIBWEBSOCKETS_WITH_LIBEVENT)
    pkg_check_modules(LIBEVENT REQUIRED libevent)

    LIST (APPEND LIBARMORYCOMMON_LINK_LIBRARIES
        ${LIBEVENT_LIBRARIES}
    )
ENDIF ()

check_cxx_symbol_exists(LWS_WITH_LIBCAP lws_config.h LIBWEBSOCKETS_WITH_LIBCAP)
IF (LIBWEBSOCKETS_WITH_LIBCAP)
    pkg_check_modules(LIBCAP REQUIRED libcap)

    LIST(APPEND LIBARMORYCOMMON_LINK_LIBRARIES
        ${LIBCAP_LIBRARIES}
    )
ENDIF ()

LIST(APPEND LIBARMORYCOMMON_INCLUDE_DIRECTORIES
    PRIVATE ${CAPNP_INCLUDE_DIRS}
)

SET(CAPNPROTO_FILES
    BDV.capnp
    Bridge.capnp
    Signer.capnp
    Types.capnp
)

#compile capnp schemas
SET(CAPNP_FILES ${PROJECT_BINARY_DIR}/cppForSwig/capnp)
FILE(MAKE_DIRECTORY ${CAPNP_FILES})

FOREACH(schema ${CAPNPROTO_FILES})
    SET(CAPNP_CC "${schema}.c++")
    ADD_CUSTOM_COMMAND(
        OUTPUT ${CAPNP_FILES}/${CAPNP_CC}
        COMMAND ${CAPNP_EXECUTABLE} compile --no-standard-import --import-path=${CAPNP_INCLUDE_DIRS} --src-prefix=${CMAKE_CURRENT_SOURCE_DIR}/capnp --output=${CAPNP_PATH}/build/${CAPNP_SUFFIX}/capnp/capnpc-c++.exe\:capnp ${CMAKE_CURRENT_SOURCE_DIR}/capnp/${schema}
    )
    LIST(APPEND LIBARMORYCOMMON_SOURCES ${CAPNP_FILES}/${CAPNP_CC})
ENDFOREACH()

ADD_LIBRARY(ArmoryCommon
    STATIC
    ${LIBARMORYCOMMON_SOURCES}
)

TARGET_COMPILE_DEFINITIONS(ArmoryCommon
    PUBLIC __STDC_LIMIT_MACROS
    ${LIBARMORYCOMMON_COMPILE_DEFINITIONS}
)

TARGET_INCLUDE_DIRECTORIES(ArmoryCommon
    ${LIBARMORYCOMMON_INCLUDE_DIRECTORIES}
)

TARGET_LINK_LIBRARIES(ArmoryCommon
    ${LIBARMORYCOMMON_LINK_LIBRARIES}
)

SET(LIBARMORYCLI_SOURCES
    ArmoryConfig.cpp
    BDM_mainthread.cpp
    BDM_Server.cpp
    BIP150_151.cpp
    BIP15x_Handshake.cpp
    BitcoinP2P.cpp
    BlockDataViewer.cpp
    BtcWallet.cpp
    HistoryPager.cpp
    HttpMessage.cpp
    JSON_codec.cpp
    LedgerEntry.cpp
    nodeRPC.cpp
    Progress.cpp
    ScrAddrObj.cpp
    Server.cpp
    StringSockets.cpp
    ZeroConf.cpp
    gtest/NodeUnitTest.cpp
)

ADD_LIBRARY(ArmoryCLI
    STATIC
    ${LIBARMORYCLI_SOURCES}
)

TARGET_INCLUDE_DIRECTORIES(ArmoryCLI
    ${LIBARMORYCOMMON_INCLUDE_DIRECTORIES}
)

TARGET_LINK_LIBRARIES(ArmoryCLI
    ArmoryCommon
)

TARGET_COMPILE_DEFINITIONS(ArmoryCLI
    PUBLIC -D__STDC_LIMIT_MACROS
)

## armorydb ##
ADD_EXECUTABLE(ArmoryDB
    main.cpp
)

SET_TARGET_PROPERTIES(ArmoryDB
    PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}
)

TARGET_INCLUDE_DIRECTORIES(ArmoryDB
    ${LIBARMORYCOMMON_INCLUDE_DIRECTORIES}
)

TARGET_LINK_LIBRARIES(ArmoryDB
    ArmoryCLI
    ${LIBWEBSOCKETS_LIB}
    ${OS_SPECIFIC_LIBS}
)

INCLUDE(GNUInstallDirs)

INSTALL(TARGETS ArmoryDB DESTINATION ${CMAKE_INSTALL_BINDIR})

## cppbridge ##
ADD_EXECUTABLE(CppBridge
    BridgeAPI/BridgeMain.cpp
    BridgeAPI/BridgeSocket.cpp
    BridgeAPI/CppBridge.cpp
    BridgeAPI/PassphrasePrompt.cpp
    BridgeAPI/ProtoCommandParser.cpp
    BridgeAPI/WalletManager.cpp
)

SET_TARGET_PROPERTIES(CppBridge
    PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}
)

TARGET_INCLUDE_DIRECTORIES(CppBridge
    ${LIBARMORYCOMMON_INCLUDE_DIRECTORIES}
)

TARGET_LINK_LIBRARIES(CppBridge
    ArmoryCommon
    ${LIBWEBSOCKETS_LIB}
    ${OS_SPECIFIC_LIBS}
)

INSTALL(TARGETS CppBridge DESTINATION ${CMAKE_INSTALL_BINDIR})

## key manager ##
ADD_EXECUTABLE(BIP150KeyManager
    KeyManager.cpp
)

SET_TARGET_PROPERTIES(BIP150KeyManager
    PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}
)

TARGET_INCLUDE_DIRECTORIES(BIP150KeyManager
    ${LIBARMORYCOMMON_INCLUDE_DIRECTORIES}
)

TARGET_LINK_LIBRARIES(BIP150KeyManager
    ArmoryCLI
    ${OS_SPECIFIC_LIBS}
)

INSTALL(TARGETS BIP150KeyManager DESTINATION ${CMAKE_INSTALL_BINDIR})

## tests ##
OPTION(ENABLE_TESTS "build tests binaries" OFF)

IF (ENABLE_TESTS)
    ADD_SUBDIRECTORY(gtest)
ENDIF ()
